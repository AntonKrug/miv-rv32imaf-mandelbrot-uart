
define print_separator
  printf "\n********************************************************************************\n" 
end


define test_passed
  print_separator
  print "PASSED the test\n"
  print_separator

  # arbirtary number just to make it different from other generic exit codes
  quit 149 
end


define test_failed
  # if it is not catched by any pass conditions, then it must be fail
  print_separator
  printf "FAILED the test\n"
  print_separator

  # arbirtary number just to make it different from other generic exit codes
  quit 150 
end


define test_failed_weak_test
  print_separator
  printf "test failing because of weak test suit\n"
  print_separator
end


define test_display_enviroment
  printf "Current build_configuration=%08X iteration=%d\n", current_configuration, $arg0
end


define connect_openocd_default
  target remote localhost:3333
end


define connect_openocd_custom
  target remote localhost:$arg0
end


define test_setup_default
  # Connect to OpenOCD / GDB server
  connect_openocd_default
  
  # Load the ELF file to the target
  load $arg0
  
  # Break in case of catastroficall fail (like a traphandler), don't forget 
  # compile with GDB_TESTING define or the Release variantion might not trigger
  # on this point even when it will be reached
  break _exit
  
  # Break at the "checkpoint" to compare the expected and actual results
  break testValidateBreak
  
  # Start executing the code
  continue
end


define assert_equals
  printf "expected=%08X actual=%08X\n", $arg0, $arg1

  # if it fails then fail the test, if it passes continue as there might be
  # more asserts
  if ($arg0 != $arg1)
    test_failed
  end
end
